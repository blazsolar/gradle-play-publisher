buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:0.5'
    }
}

apply plugin: 'groovy'
apply plugin: 'idea'
apply plugin: 'maven'
apply plugin: 'com.jfrog.bintray'

group projectGroup
version projectVersion

repositories {
    mavenCentral()
}

dependencies {
    compile gradleApi()
    compile localGroovy()
    compile 'com.google.apis:google-api-services-androidpublisher:v2-rev2-1.19.0'
    compile 'com.google.oauth-client:google-oauth-client-java6:1.19.0'
    compile 'com.google.oauth-client:google-oauth-client-jetty:1.19.0'
}

configurations {
    published
}

task sourceJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

// Add the sourceJars to non-extractor modules
artifacts {
    published sourceJar
    published javadocJar
}

if (!project.hasProperty("bintrayUser")) {
    ext.bintrayUser = ""
}

if (!project.hasProperty("bintrayKey")) {
    ext.bintrayKey = ""
}

bintray {
    user = bintrayUser
    key = bintrayKey

    configurations = ['published', 'archives']

    pkg {
        repo='gradle-plugins'
        name=projectName
        desc='Gradle plugin for modifying google play app listings.'
        websiteUrl='https://github.com/blazsolar/gradle-play-publisher'
        issueTrackerUrl='https://github.com/blazsolar/gradle-play-publisher/issues'
        vcsUrl='https://github.com/blazsolar/gradle-play-publisher.git'
        licenses=['MIT']
        labels=['google-play', 'publisher']

        version {
            name=projectVersion
            attributes=['gradle-plugin':'com.github.blazsolar.play-publisher:' + projectGroup + ':' + projectName]
        }
    }

}